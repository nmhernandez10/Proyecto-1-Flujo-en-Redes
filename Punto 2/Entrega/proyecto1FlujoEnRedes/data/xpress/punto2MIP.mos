model ModelName
uses "mmxprs"; !gain access to the Xpress-Optimizer solver
uses "mmsystem";


!sample declarations section
declarations
N: set of integer
K: set of string

!parametros
c: dynamic array(N,N) of real
n: integer
C:integer
D: array(N) of integer
V: integer					!Numero de vehiculos a usar en el problema
Mf:real
Mv:real
Mt:real
Cm:real
granM:integer
b: array(N,K) of integer

!Variables de decision
x: dynamic array (N,N,K) of mpvar

end-declarations

initializations from "parameters.txt"
	n V Mf Mv Mt Cm C granM
end-initializations

initializations from "b.txt"
	b
end-initializations

procedure readadyacencia(strin:string, fila:integer, columna:integer)
	num := parseint(strin,1)
	if num>0 then
		create(x(fila,columna,'helados'))
		create(x(fila,columna,'vehiculos'))
		x(fila,columna,'vehiculos') is_binary
	end-if
end-procedure

procedure readCost(strin:string, fila:integer,columna:integer)
	if exists(x(fila,columna,'helados')) then
		c(fila,columna):= parsereal(strin,1)
	end-if
end-procedure

procedure initialize
	fopen("adyacencias.txt",F_INPUT)
		j:=""
		contadorF:=0
		contadorC:=0
		while(not iseof) do
			contadorC+=1
			read(j,"\t")
			if contadorC> V+n+1 then
				contadorC:=0
				contadorF+=1
			end-if
			if contadorF>0 and contadorC>0 then
				readadyacencia(j,contadorF,contadorC)	
			end-if
		end-do
	fclose(F_INPUT)
	
	fopen("costos.txt",F_INPUT)
		j:=""
		contadorF:=0
		contadorC:=0
		while(not iseof) do
			contadorC+=1
			read(j,"\t")
			if contadorC> V+n+1 then
				contadorC:=0
				contadorF+=1
			end-if
			if contadorF>0 and contadorC>0 then
				readCost(j,contadorF,contadorC)	
			end-if
		end-do
	fclose(F_INPUT)
	
end-procedure


initialize 

!Restriccion balance
forall(i in N, k in K) do
	sum(j in N | exists(x(i,j,k)))x(i,j,k) -sum(j in N | exists(x(j,i,k)))x(j,i,k) = b(i,k)
end-do

forall(i in N)do
	sum(j in N | exists(x(i,j,'vehiculos'))and j<>19)x(i,j,'vehiculos')<=1
	sum(j in N | exists(x(j,i,'vehiculos')) and i<>19)x(j,i,'vehiculos')<=1
end-do

!Restriccion de capacidad
forall(i,j in N | exists(x(i,j,'helados')))do
	x(i,j,'helados') <= x(i,j,'vehiculos')*C
end-do
!Restriccion 

Fo:= sum(i,j in N | exists(x(i,j,'vehiculos')))c(i,j)*x(i,j,'vehiculos')

minimize(Fo)

procedure imprimirResultados
	fopen("resultadosMIP.txt",F_OUTPUT)
	forall(i,j in N| getsol(x(i,j,'vehiculos'))>0) do
		if j=19 and i>V then
			writeln("Se devuelve hacia la bodega desde cliente ",i-V)	
		elif j=19 and i<=V then
			writeln("No se utiliza el camion ",i)	
		elif i<=V then
			writeln("Sale camion ",i," hasta cliente ", j-V)				
		else
			writeln("Va de cliente ",i-V," hasta cliente ", j-V)		
		end-if
	end-do
	writeln("El costo total de transporte sin incluir demoras es de $",getobjval)
	fclose(F_OUTPUT)
end-procedure

imprimirResultados


end-model
